apply plugin: 'com.android.library'

def localProps = new Properties()
def localFile = rootProject.file('local.properties')

if (localFile.exists()) {
    localFile.withInputStream { stream -> localProps.load(stream) }
} else {
    println "Warning: local.properties not found"
}
// 从 local.properties 读取 RTC SDK 版本配置: "3" 使用 3.7.x 版本, "4" 使用 4.x 版本
def rtcVersion = localProps.getProperty('app.rtcVersion', '4')

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 将 RTC 版本配置传递给 Java 代码
        buildConfigField "String", "RTC_VERSION", "\"${rtcVersion}\""
    }

    viewBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 根据版本配置排除不需要的源文件
    sourceSets {
        main {
            java {
                if (rtcVersion == "3") {
                    // 使用 3.x 版本时，排除 rtc4 目录下的所有文件
                    exclude '**/rtc4/**'
                } else {
                    // 使用 4.x 版本时，排除 rtc3 目录下的所有文件
                    exclude '**/rtc3/**'
                }
            }
        }
    }
}

dependencies {
    implementation project(':sdk')
    // implementation 'com.github.netless-io:whiteboard-android:2.16.43'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // 根据版本配置引入对应的 RTC SDK
    def rtcSdk3 = "io.agora.rtc:full-rtc-basic:3.7.1"
    def rtcSdk4 = "io.agora.rtc:full-sdk:4.3.2"
    compileOnly rtcSdk3
    compileOnly rtcSdk4
    implementation(rtcVersion == "3" ? rtcSdk3 : rtcSdk4)

    implementation 'androidx.appcompat:appcompat:1.2.0'

    // test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}
